# (C) முத்தையா அண்ணாமலை 2017
# இது ஒரு எழில் தமிழ் நிரலாக்க மொழி உதாரணம்

நிரல்பாகம் மரம்_செய்( அளவு )
  # left, right,value
   ம = {"இடது_நுனி": [],"வலது_நுனி": [], "மதிப்பு":அளவு}
   பின்கொடு ம
முடி

நிரல்பாகம் வலது_நுனி_செய்( வேர்நுனி, நுனி )
   வேர்நுனி["வலது_நுனி"] = நுனி
முடி

நிரல்பாகம் இடது_நுனி_செய்( வேர்நுனி, நுனி )
   வேர்நுனி["இடது_நுனி"] = நுனி
முடி

# walk in in-order
நிரல்பாகம் வரிசையில்_எடு(வேர்,ப)
    @( வேர்["இடது_நுனி"]  != [] )  ஆனால்
        வரிசையில்_எடு( வேர்["இடது_நுனி"] , ப)
    முடி
    
    பதிப்பி வேர்["மதிப்பு"] 
    பின்இணை( ப, வேர்["மதிப்பு"] )
    
    @( வேர்["வலது_நுனி"] != [] )  ஆனால்
        வரிசையில்_எடு( வேர்["வலது_நுனி"] , ப)
    முடி
    
    பின்கொடு ப
முடி

#walk pre-order
நிரல்பாகம் தன்முன்_வரிசையில்_எடு(வேர்,ப)

    பதிப்பி "%d,", வேர்["மதிப்பு"]
    பின்இணை( ப, வேர்["மதிப்பு"] )

    @( வேர்["இடது_நுனி"] != [] ) ஆனால்
    தன்முன்_வரிசையில்_எடு( வேர்["இடது_நுனி"] , ப)
    முடி

    @( வேர்["வலது_நுனி"] != [] ) ஆனால்
    தன்முன்_வரிசையில்_எடு( வேர்["வலது_நுனி"] , ப)
    முடி

    பின்கொடு ப
முடி

#walk post-order
நிரல்பாகம் தன்பின்_வரிசையில்_எடு(வேர்,ப)
    @( வேர்["இடது_நுனி"] != [] ) ஆனால்
    தன்பின்_வரிசையில்_எடு( வேர்["இடது_நுனி"] , ப)
    முடி

    @( வேர்["வலது_நுனி"] != [] ) ஆனால்
    தன்பின்_வரிசையில்_எடு( வேர்["வலது_நுனி"] , ப)
    முடி

    பதிப்பி "%d,", வேர்["மதிப்பு"]
    பின்இணை( ப, வேர்["மதிப்பு"] )

    பின்கொடு ப
முடி

#height of tree
நிரல்பாகம் தன்_உயரம்(வேர்)
    இடது_உயரம் = 0
    வலது_உயரம் = 0
    @( வேர்["இடது_நுனி"] != [] ) ஆனால்
        இடது_உயரம் = தன்_உயரம்( வேர்["இடது_நுனி"])
    முடி

    @( வேர்["வலது_நுனி"] != [] ) ஆனால்
        வலது_உயரம்= தன்_உயரம்( வேர்["வலது_நுனி"])
    முடி

    பின்கொடு max( இடது_உயரம்,வலது_உயரம்) + 1
முடி

#find leaves
நிரல்பாகம் இலை_நுனிகளை_எடு(வேர்,ப)
    இடது_காலி = 0
    @( வேர்["இடது_நுனி"] != [] ) ஆனால்
        இலை_நுனிகளை_எடு( வேர்["இடது_நுனி"] , ப)
    இல்லை 
        இடது_காலி = 1
    முடி
    வலது_காலி = 0
    @( வேர்["வலது_நுனி"] != [] ) ஆனால்
        இலை_நுனிகளை_எடு( வேர்["வலது_நுனி"] , ப)
    இல்லை 
        வலது_காலி = 1
    முடி
    @( வலது_காலி && இடது_காலி) ஆனால்
        பதிப்பி "%d,", வேர்["மதிப்பு"]
        பின்இணை( ப, வேர்["மதிப்பு"] )
    முடி
    பின்கொடு ப
முடி

# search for value
நிரல்பாகம் மரம்_தேடு(வேர்,மதிப்பு)
    வலது = 0
    இடது = 0
    @( வேர்["இடது_நுனி"] != [] ) ஆனால்
        இடது = மரம்_தேடு ( வேர்["இடது_நுனி"] , மதிப்பு  )
    முடி
    @( வேர்["வலது_நுனி"] != [] ) ஆனால்
        வலது = மரம்_தேடு (  வேர்["வலது_நுனி"] ,மதிப்பு )
    முடி
    @( வலது || இடது ) ஆனால்
        பின்கொடு (வலது || இடது)
    முடி
    
    பின்கொடு வேர்["மதிப்பு"] == மதிப்பு
முடி

# இரட்டித்த மரம் நடுவோம் - கட்டுமானம் 
வேர் = மரம்_செய்(2)
நுனி5 = மரம்_செய்(5)
நுனி7 = மரம்_செய்(7)
வலது_நுனி_செய்(வேர்,நுனி5)
இடது_நுனி_செய்(வேர்,நுனி7)

நுனி9 = மரம்_செய்(9)
நுனி4 = மரம்_செய்(4)
வலது_நுனி_செய்(நுனி5,நுனி9)
இடது_நுனி_செய்(நுனி9,நுனி4)

நுனி2 = மரம்_செய்(2)
நுனி6 = மரம்_செய்(6)
வலது_நுனி_செய்(நுனி7,நுனி6)
இடது_நுனி_செய்(நுனி7,நுனி2)

நுனி11 = மரம்_செய்(11)
நுனி5 = மரம்_செய்(5)
வலது_நுனி_செய்(நுனி6,நுனி11)
இடது_நுனி_செய்(நுனி6,நுனி5)

# மரம் நுனிகளை அனைத்தயும் வரிசையில் எடுப்பது
ம_வரிசை = பட்டியல்()
வரிசையில்_எடு( வேர், ம_வரிசை )
பதிப்பி ம_வரிசை

ம_வரிசை = பட்டியல்()
வரிசையில்_எடு( நுனி7, ம_வரிசை )
பதிப்பி ம_வரிசை

ம_வரிசை = பட்டியல்()
வரிசையில்_எடு( நுனி5, ம_வரிசை )
பதிப்பி ம_வரிசை

ம_வரிசை = பட்டியல்()
தன்முன்_வரிசையில்_எடு(வேர், ம_வரிசை )
பதிப்பி ம_வரிசை

height = தன்_உயரம்(வேர்)
பதிப்பி height

இலை_ப = பட்டியல்()
இலை_நுனிகளை_எடு(வேர், இலை_ப )
பதிப்பி இலை_ப

மதிப்பு1 = 10
மதிப்பு2 = 1000
உள்ளதா1 = மரம்_தேடு(வேர், மதிப்பு1 )
உள்ளதா2 = மரம்_தேடு(வேர், மதிப்பு2 )
பதிப்பி மதிப்பு1, உள்ளதா1
பதிப்பி மதிப்பு2, உள்ளதா2
